import com.tminus1010.tmcommonkotlin.Shared

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 26
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.tminus1010.tmcommonkotlin.customviews'
}

// TODO: cleanup
dependencies {
    // # This Project
    implementation(project(":tmcommonkotlin-core"))
    api(project(":tmcommonkotlin-rx3"))
    implementation(project(":tmcommonkotlin-coroutines"))
    implementation(project(":tmcommonkotlin-androidx"))
    // # Kotlin
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.21")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.7.21")
    // # Android
    implementation("androidx.core:core-ktx:1.3.2")
    implementation("androidx.appcompat:appcompat:1.2.0")
    // # Rx
    api("io.reactivex.rxjava3:rxjava:3.0.7")
    api("io.reactivex.rxjava3:rxandroid:3.0.0")
    // ## Rx+LiveData
    implementation("androidx.lifecycle:lifecycle-reactivestreams:2.4.1")
    // # Navigation
    implementation("androidx.navigation:navigation-fragment-ktx:2.4.2")
    implementation("androidx.navigation:navigation-ui-ktx:2.4.2")
    // # Apache Commons
    implementation("commons-io:commons-io:2.8.0")
    // # Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"
    // # Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    // # ViewBinding
    implementation("androidx.databinding:viewbinding:7.1.3")
    // # Constraint Layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
}

tasks.register("sourceJar", Jar) {
    from("src/main")
}

afterEvaluate {
    publishing {
        publications {
            register("mavenPublication", MavenPublication) {
                artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
                artifact(tasks.getByName("sourceJar")) { classifier = "sources" }
                groupId = Shared.groupId
                artifactId = "tmcommonkotlin-customviews"
                version = rootProject.ext["versionName"]
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        def group = ""
                        if (it.group == "TMCommonKotlin")
                            group = Shared.groupId
                        else
                            group = it.group
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}