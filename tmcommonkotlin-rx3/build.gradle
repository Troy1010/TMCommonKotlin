import com.tminus1010.tmcommonkotlin.Shared

plugins {
    id("com.android.library")
    id("kotlin-android")
    id("maven-publish")
}

android {
    compileSdk = Shared.targetSDK

    compileOptions.sourceCompatibility = JavaVersion.VERSION_1_8
    compileOptions.targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions.jvmTarget = "1.8"

    defaultConfig {
        minSdkVersion(Shared.minSDK)
        targetSdkVersion(Shared.targetSDK)
        versionCode = Shared.versionCode
        versionName = Shared.versionName

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        getByName("release") {
            minifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
}

dependencies {
    // # This Project
    api(project(":tmcommonkotlin-tuple"))
    api(project(":tmcommonkotlin-core"))
    // # Kotlin
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.21")
    // # JUnit
    testImplementation("junit:junit:4.13.2")
    // # Mockk
    testImplementation("io.mockk:mockk:1.11.0")
    // # Rx
    api("io.reactivex.rxjava3:rxjava:3.0.7")
    // ## Rx & Android
    api("io.reactivex.rxjava3:rxandroid:3.0.0")
    // ## Rx & Kotlin
    api("io.reactivex.rxjava3:rxkotlin:3.0.1")
    // ## Rx & Lifecycle
    api("com.trello.rxlifecycle4:rxlifecycle-android-lifecycle-kotlin:4.0.0")
}


tasks.register("sourceJar", Jar) {
    from("src/main")
}

afterEvaluate {
    publishing {
        publications {
            register("mavenPublication", MavenPublication) {
                artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
                artifact(tasks.getByName("sourceJar")) { classifier = "sources" }
                groupId = Shared.groupId
                artifactId = "tmcommonkotlin-rx3"
                version = Shared.versionName
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        def group = ""
                        if (it.group == "TMCommonKotlin")
                            group = Shared.groupId
                        else
                            group = it.group
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}