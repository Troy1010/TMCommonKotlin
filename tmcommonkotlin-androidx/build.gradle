import com.tminus1010.tmcommonkotlin.Shared

plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-kapt")
    id("maven-publish")
}

android {
    compileSdk = Shared.targetSDK

    compileOptions.sourceCompatibility = JavaVersion.VERSION_11
    compileOptions.targetCompatibility = JavaVersion.VERSION_11
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()

    defaultConfig {
        minSdkVersion(Shared.minSDK)
        targetSdkVersion(Shared.targetSDK)

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        getByName("release") {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.tminus1010.tmcommonkotlin.androidx'
}

dependencies {
    // # This Project
    implementation(project(":tmcommonkotlin-core"))
    api(project(":tmcommonkotlin-rx3"))
    implementation(project(":tmcommonkotlin-coroutines"))
    api(project(":tmcommonkotlin-view"))
    // # Kotlin
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.21")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.7.21")
    // # Android
    implementation("androidx.core:core-ktx:1.3.2")
    implementation("androidx.appcompat:appcompat:1.2.0")
    // # RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    // # Rx
    api("io.reactivex.rxjava3:rxjava:3.0.7")
    api("io.reactivex.rxjava3:rxandroid:3.0.0")
    // # Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    // # ViewBinding
    implementation("androidx.databinding:viewbinding:7.1.3")
    // # Constraint Layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    // # Inject
    implementation 'javax.inject:javax.inject:1'
}

tasks.register("sourceJar", Jar) {
    from("src/main")
}

afterEvaluate {
    publishing {
        publications {
            register("mavenPublication", MavenPublication) {
                artifact bundleReleaseAar
                artifact sourceJar
                groupId = Shared.groupId
                artifactId = "tmcommonkotlin-androidx"
                version = rootProject.ext["versionName"]
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        def group = ""
                        if (it.group == "TMCommonKotlin")
                            group = Shared.groupId
                        else
                            group = it.group
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
