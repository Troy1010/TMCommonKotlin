import com.tminus1010.tmcommonkotlin.Shared

plugins {
    id("com.android.library")
    id("kotlin-android")
    id("maven-publish")
}

android {
    compileSdk = Shared.targetSDK
    buildToolsVersion = "30.0.3"

    compileOptions.sourceCompatibility = JavaVersion.VERSION_1_8
    compileOptions.targetCompatibility = JavaVersion.VERSION_1_8
    kotlinOptions.jvmTarget = "1.8"

    defaultConfig {
        minSdkVersion(Shared.minSDK)
        targetSdkVersion(Shared.targetSDK)
        versionCode = Shared.versionCode
        versionName = Shared.versionName

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        getByName("release") {
            minifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
}

dependencies {
    // # This Project
    api(project(":tmcommonkotlin-core"))
    // # Kotlin
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.10")
    // # JUnit
    testImplementation("junit:junit:4.13.2")
    // # Mockk
    testImplementation("io.mockk:mockk:1.12.7")
    // # Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    // ## Coroutine & Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
}


tasks.register("sourceJar", Jar) {
    from("src/main")
}

afterEvaluate {
    publishing {
        publications {
            register("mavenPublication", MavenPublication) {
                artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
                artifact(tasks.getByName("sourceJar")) { classifier = "sources" }
                groupId = Shared.groupId
                artifactId = "tmcommonkotlin-coroutines"
                version = Shared.versionName
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        def group = ""
                        if (it.group == "TMCommonKotlin")
                            group = Shared.groupId
                        else
                            group = it.group
                        dependencyNode.appendNode('groupId', group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}